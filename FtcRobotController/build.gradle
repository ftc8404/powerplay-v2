import java.text.SimpleDateFormat

apply plugin: 'com.android.library'

android {

    namespace 'com.qualcomm.ftcrobotcontroller'
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28

        buildConfigField "String", "APP_BUILD_TIME", '"' + (new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ", Locale.ROOT).format(new Date())) + '"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    ndkVersion '21.3.6528147'
}

apply from: '../moduleFlavors.gradle'

dependencies {

    api 'androidx.appcompat:appcompat:1.5.1'

    /*
     * Other Modules
     */
    turboApi project(':FtcCommon')
    turboApi project(':RobotCore')
    turboApi project(':Inspection')
    turboApi project(':Blocks')
    turboApi project(':Hardware')
    turboApi project(':RobotServer')
    turboApi project(':OpenRC')

    extremeTurboApi project(':FtcCommon')
    extremeTurboApi project(':RobotCore')
    extremeTurboApi project(':Inspection')
    extremeTurboApi project(':Hardware')
    extremeTurboApi project(':OpenRC')
    extremeTurboApi project(':RobotServer')

    /*
     * After the migration of the official libs to maven central,
     * robotcore now has a transitive dependency on the FTC TFOD
     * lib, which has a transitive dependency on the actual TFOD
     * lib, which has a transitive dependency on appcompat-v7.
     *
     * Because all the other libs (e.g. Inspection) depend on RobotCore,
     * they in turn all end up having a transitive dependency on the
     * appcompat-v7 package too. So, we have to specifically exclude
     * it from *every* single FTC lib. Sigh.
     */
    stockApi ('org.firstinspires.ftc:Inspection:8.1.1') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    stockApi ('org.firstinspires.ftc:Blocks:8.1.1') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    stockApi ('org.firstinspires.ftc:RobotCore:8.1.1') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    stockApi ('org.firstinspires.ftc:RobotServer:8.1.1') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    stockApi ('org.firstinspires.ftc:OnBotJava:8.1.1') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    stockApi ('org.firstinspires.ftc:Hardware:8.1.1') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    stockApi ('org.firstinspires.ftc:FtcCommon:8.1.1') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }

    stockApi 'org.firstinspires.ftc:gameAssets-PowerPlay:1.0.0'
    /*
     * External
     */
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
